#!/bin/csh
# CVS: $Id$

# Name:   cvs-add-rundir
# Author: wd (Wolfgang.Dobler@kis.uni-freiburg.de)
# Date:   22-Jul-2002
# Description:
#   Add the current run directory to the CVS repository. Assumes that the
#   directory above (`..') is already under CVS.
# Usage:
#   [create new run directory; pc_setupsrc; mkinpars; tune parameters]
#   newrundir>  cvs-add-rundir

set rundir = `pwd`
set commit = 0
set cmdname = $0
set cmdname = $cmdname:t

if ($#argv > 0) then
  if (("$1" == "-h") || ("$1" == "--help")) then
    goto usage
  endif
  if (("$1" == "-c") || ("$1" == "--commit")) then
    set commit = 1
  endif
endif

(cd ..; cvs add $rundir:t)
cvs add *.in
cvs add src
cvs add src/*.local

if (-e k.dat) cvs add k.dat
if (-e stratification.dat) cvs add stratification.dat
if (-e prof_lnrho.dat) cvs add prof_lnrho.dat
if (-e prof_lnT.dat) cvs add prof_lnT.dat
if (-e driver) cvs add driver
if (-e driver/mag_field.dat) cvs add driver/mag_field.dat

if ($commit) then
  cvs commit
endif

exit:
exit

usage:

echo "Usage:"
echo "  $cmdname [-c|--commit]"
echo "Add the current run directory to the CVS repository. Assumes that the"
echo "directory above (..) is already under CVS."
echo "With -c (--commit) do 'cvs commit', too."

# End of file cvs-add-rundir
